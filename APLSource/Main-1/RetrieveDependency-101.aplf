 RetrieveDependency←{
     ⍝ ⍺ ←→ Project Folder
     ⍝ ⍵ ←→ Dependency Space
     ⍝ ← ←→ Project or Package Config Space
     ⍝ Do not replace ⎕CMD below with RunProcess. See issue #53
     0=⍵.⎕NC'URL':⍵ DependencyError'No URL specfied'
     0=⍵.⎕NC'Version':⍵ DependencyError'No version specified'
     u←⍵.URL
     v←⍵.Version
     lp←⍵.LocalProjects
     jf←(lp.URL⍳⊂u)⊃(lp.ProjectFolder),⊂''
     ⍵.ProjectOverride∧jf≢'':⍵ GetConfigSpace jf
     kf←PackageFQP ⍵
     ⎕NEXISTS kf:⍵ GetConfigSpace kf
     u←(jf≢'')⊃u('file://',jf)
     kn←∊1↓⎕NPARTS ¯1↓kf
     ⎕←'Retrieving "',kn,'" from ',u,'...'
     u←⍺{
         'file'≡4↑⍵:⍵
         t←GetGithubToken ⍺,'../'
         t,←(0≠≢t)↑'@'
         'https://',t,8↓⍵
     }u
     sw←'--depth 1 --branch ',v
     q←{'"',⍵,'"'}
     l←1↓∊' ',¨'clone'sw(q tu)(q kf)
     r←⎕CMD'git ',l
     _←DeleteGitItems kf
     c←⍵ GetConfigSpace kf
     _←DeleteDevSpaceFolders c
     _←c.IsApplication DeleteDevFolders c
     c
 }
