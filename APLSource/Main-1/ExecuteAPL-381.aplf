 ExecuteAPL←{
     ⍝ ⍺ ←→ [Executable] [Arguments]
     ⍝      Executable defaults to current session executable
     ⍝                 (use '' to default if providing args)
     ⍝      Arguments e.g.: '-b maxws=1g default_io=1'
     ⍝ ⍵ ←→ One or more expressions to execute
     ⍝ ← ←→ Exit Code, Session Output
     ⍺←''
     (x a)←2↑(⊆⍺),⊂'ShowStatusOnError=0 Dyalog_LineEditor_Mode=1'
     cx aa←2↑(2 ⎕NQ'.' 'GetCommandLineArgs'),⊂''
     in←∊(⊆⍵),¨⎕UCS 13
     _net←⎕NS''
     _net.⎕USING←{
         ⍵:'System' 'System.IO,System.Runtime.Extensions' 'System.Diagnostics,System.Diagnostics.Process'
         'System' 'System.IO' 'System.Diagnostics,System.dll'
     }IsNetCore''
     r←⎕NS''
     p←⎕NEW _net.Process
     i←p.StartInfo
     i.FileName←x cx⊃⍨0=≢x
     i.Arguments←aa,' ',a
     i.RedirectStandardOutput←1
     i.RedirectStandardInput←1
     i.RedirectStandardError←1
     i.UseShellExecute←0
     _←p.Start ⍬
     sw←⎕NEW _net.StreamWriter p.StandardInput.BaseStream
     op←{6::0 ⋄ ⍺⍺ ⍵}
     _←sw.Write op⊂in
     _←{6::0 ⋄ z←sw.Close}0
     r←⎕NS''
     sr←⎕NEW _net.StreamReader p.StandardError.BaseStream
     r.SessionOutput←{(~' '∧.=¨⍵)/⍵}{⍵⊆⍨~⍵∊⎕UCS 13 10}sr.ReadToEnd
     _←p.WaitForExit op ⍬
     r.ExitCode←p.ExitCode
     r
 }
