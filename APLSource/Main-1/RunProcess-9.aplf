 RunProcess←{
     ⍝ ⍺ ←→  Format result like ⎕CMD or not
     ⍝ ⍵[0] ←→ Executable
     ⍝ ⍵[1] ←→ Argument
     ⍺←1
     c←'1'≡↑2 ⎕NQ'.' 'GetEnvironment' 'DYALOG_NETCORE'
     c∨←'W'≠↑0⊃'#'⎕WG'APLVersion'
     _net←⎕NS''
     _net.⎕USING←{
         ~c:'System' 'System.IO' 'System.Diagnostics,System.dll'
         u←'' 'System'
         u,←⊆'System.IO,System.Runtime.Extensions'
         u,←⊂'System.Diagnostics,System.Diagnostics.Process'
         u}c
     r←⎕NS''
     p←⎕NEW _net.Process
     i←p.StartInfo
     i.FileName←0⊃⍵
     i.Arguments←1⊃⍵
     i.RedirectStandardOutput←1
     i.RedirectStandardInput←1
     i.RedirectStandardError←1
     i.UseShellExecute←0
     i.CreateNoWindow←1
     _←p.Start ⍬
     re←⎕NEW _net.StreamReader p.StandardError.BaseStream
     ro←⎕NEW _net.StreamReader p.StandardOutput.BaseStream
     t←r{⍺.StdErr←⍵.ReadToEnd}&re
     r.StdOut←ro.ReadToEnd
     op←{6::0 ⋄ z←⍺⍺ ⍵}
     _←p.WaitForExit op ⍬
     _←⎕TSYNC op t
     r.ExitCode←p.ExitCode
     ~⍺:r
     rtc←{' '@((⎕UCS 9)∘=)⍵}
     pfn←{⍵⊆⍨~⍵∊⎕UCS 10 13}
     r.StdOut←pfn rtc r.StdOut
     r.StdErr←pfn rtc r.StdErr
     r
 }
