 GitHubReleaseUploadAsset←{
    ⍝ Upload release file asset to existing release
    ⍝ ⍺ ←→ Property space
    ⍝ ⍵ ←→ Filepath
     gct←{
         e←⎕C⊃⌽⎕NPARTS ⍵
         e≡'.dll':'application/octet-stream'
         e≡'.msi':'application/x-msdownload'
         e≡'.zip':'application/zip'
         'application/octet-stream'
     }

     percentEncode←{
         r←⊂'%20'
         f←' '∘=
         ∊r@f ⍵
     }

     rawread←{              ⍝ Read native file ⍵
         t←⍵ ⎕NTIE 0        ⍝ Tie w/nxt avail num<
         s←⎕NSIZE t         ⍝ Bytes
         b←⎕NREAD t 83 s 0  ⍝ Read em all
         t←⎕NUNTIE t        ⍝ Untie it
         b                  ⍝ Return
     }

     http←⎕SE.SALT.New'HTTPCommand'
     http.Headers←1 2⍴'Accept' 'application/vnd.github.v3+json'
     http.Headers⍪←'Content-Type'(gct ⍵)
     http.Headers⍪←'Authorization'('token ',⍺.GitToken)
     http.Params←80 ⎕DR rawread ⍵
     http.Command←'POST'
     http.WaitTime←600
     http.URL←¯13↓⍺.AssetsUploadURL
     http.URL,←'?name=',percentEncode∊1↓⎕NPARTS ⍵
     r←http.Run
     201≠r.HttpStatus:'Error GitHub release asset not uploaded.'⎕SIGNAL 11
     0
 }
