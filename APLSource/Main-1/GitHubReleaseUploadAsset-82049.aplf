 GitHubReleaseUploadAsset←{

     gct←{
         e←0(819⌶)2⊃⎕NPARTS ⍵
         e≡'.dll':'application/octet-stream'
         e≡'.msi':'application/x-msdownload'
         e≡'.zip':'application/zip'
         'application/octet-stream'
     }⍵

     rawread←{              ⍝ Read native file ⍵
         t←⍵ ⎕NTIE 0        ⍝ Tie w/nxt avail num<
         s←⎕NSIZE t         ⍝ Bytes
         b←⎕NREAD t 82 s 0  ⍝ Read em all
         t←⎕NUNTIE t        ⍝ Untie it
         b                  ⍝ Return
     }

     http←⎕SE.SALT.New'HTTPCommand'
     http.Headers←1 2⍴'Accept' 'application/vnd.github.v3+json'
     http.Headers⍪←'Content-Type'gct
     http.Headers⍪←'Authorization'('token ',⍺.GitToken)
     http.Params←rawread ⍵
     http.(Params←⎕UCS'UTF-8'⎕UCS Params)
     http.Command←'POST'
     http.URL←¯13↓⍺.AssetsUploadURL
     http.URL,←'?name=',∊1↓⎕NPARTS ⍵
     r←http.Run
     201≠r.HttpStatus:'Error GitHub release asset not uploaded.'⎕SIGNAL 11
     0
 }
