 GetBuildID←{
     s←⍺
     0=≢{6::'' ⋄ ⍵.OnBuild}s:0
     p←s.ProjectFolder
     q←{6::0 ⋄ ⍵.Public}s
     n←'BuildIDs',⍨q⊃'Private' 'Public'
     u←n,⍨↑⎕NPARTS s.URL
     d←s.ProjectsFolder,'.',n
     t←GetGithubToken s.ProjectsFolder
     r←t DownloadProject u d
     ∨/r∊⊂'remote: Repository not found.':0
     f←d,'/BuildIDs.json'
     b←{0::'' ⋄ ⎕JSON↑⎕NGET ⍵}f
     in id←{0::⊂'' ⋄ ⍵.ProjectName ⍵.BuildID}b
     pn←s._Name
     ne←~in∊⍨⊂pn
     in,←ne/⊂pn
     id,←ne/0
     i←in⍳⊂pn
     id[i]+←1
     b←⎕NS¨(≢in)⍴⊂''
     b.ProjectName b.BuildID←in id
     _←b nputJSON f
     _←d Commit'Increment build ID for ',pn,' v ',s.Version,' to ',⍕id[i]
     _←d Push''
     id[i]
 }
